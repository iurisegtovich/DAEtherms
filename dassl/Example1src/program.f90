!EXEMPLO DE UTILIZAÇÃO DA SUBROTINA DASSL PARA INTEGRAÇÃO NUMÉRICA DE DAEs
!O CÓDIGO AQUI PRESENTE RESOLVE UM PROBLEMA DE ENCHIMENTO DE TANQUE SIMPLES
!ATUALIZAÇÕES E MAIS INFORMAÇÕES PODEM SER ENCONTRADAS EM
!github.com/hermesribeiro/tutorialdassl
!É ALTAMENTE RECOMENDADA A LEITURA DO PRÓLOGO PRESENTE NO ARQUIVO "ddassl.f"
!CONTRIBUIDORES: Hermes R. Sant' Anna, Iuri Segtovich 

Program main
    !DECLARAÇÃO DE VARIÁVEIS PARA A DASSL
    !PARA EVITAR ERROS EM ALGUNS COMPILADORES, AS VARIÁVEIS IPAR, RPAR, RTOL, ATOL DEVEM SER DECLARADAS COMO VETORES
    !MAIS INFORMAÇÕES SOBRE AS DECLARAÇÕES DE VARIÁVEIS PODEM SER ENCONTRADAS NA ROTINA "ddassl.f"
    implicit none
    EXTERNAL RES, JAC
    INTEGER NEQ, INFO(15), IDID, LRW, LIW, IWORK(22), IPAR(1)
    DOUBLE PRECISION T, Y(2), YPRIME(2), TOUT, RTOL(2), ATOL(2), RWORK(62), RPAR(3)
    !DECLARAÇÃO DE VARIÁVEIS DO PROBLEMA
    DOUBLE PRECISION NL, NI, dNL, dNI, KTRANSCAL, TSIS, TFREEZ
    DOUBLE PRECISION HPRIME,FOPRIME
    DOUBLE PRECISION DT, TEND
    !VARIÁVEIS AUXILIARES DO PROGRAMA
    INTEGER :: saidaFnum
    
    !ATRIBUINDO VALORES ÀS CONSTANTES
    KTRANSCAL = 1E-2
    TSIS=298.
    TFREEZ=273.
    
    !DEFININDO CONDIÇÕES INICIAIS DO PROCESSO
    NL=1.
    NI=.5
    
    !DEFININDO DERIVADAS TEMPORAIS INICIAIS PARA O MÉTODO NUMÉRICO
    !NA AUSÊNCIA DE ESTIMATIVAS INICIAIS, INSIRA UM VALOR APROXIMADO QUE A DASSL TENTA ESTIMÁ-LAS
    dNL=KTRANSCAL*(TSIS-TFREEZ)
    dNI=-1*dNL
    
    !INICIANDO OS PARÂMETROS DA DASSL
    T=0.D0                  !TEMPO ATUAL (TEMPO INICIAL NESSE CASO)
    DT=1.D-1                !TAMANHO DO PASSO NO TEMPO
    TEND=20.D0              !DURAÇÃO DA SIMULAÇÃO DO ENCHIMENTO DE TANQUE
    Y(1)=NL                  !PASSAGEM DA CONDIÇAO INICIAL PARA UM VETOR LEGÍVEL PELA DASSL
    Y(2)=NI                 !PASSAGEM DA CONDIÇAO INICIAL PARA UM VETOR LEGÍVEL PELA DASSL
    YPRIME(1)=dNL        !PASSAGEM DA CONDIÇAO INICIAL PARA UM VETOR LEGÍVEL PELA DASSL   
    YPRIME(2)=dNI       !PASSAGEM DA CONDIÇAO INICIAL PARA UM VETOR LEGÍVEL PELA DASSL
    TOUT=T+DT               !TEMPO FINAL DDO PRIMEIRO PASSO DA INTEGRAÇÃO NO TEMPO
    INFO=0                  !VETOR CONTENDO OPÇÓES DA DASSL. DEFAULT: INFO=0
    RTOL=1.D-6              !TOLERÂNCIA RELATIVA
    ATOL=1.D-8              !TOLERÂNCIA ABSOLUTA
    NEQ=2                   !NÚMERO DE EQUAÇÕES
    LIW=22                  !O CÁLCULO DESTE VALOR É ENSINADO NO CÓDIGO DA DASSL
    LRW=62                  !O CÁLCULO DESTE VALOR É ENSINADO NO CÓDIGO DA DASSL
    !PASSAGEM DE VARIÁVEIS PARA A SUBROTINA RES()
    RPAR(1)=KTRANSCAL
    RPAR(2)=TSIS
    RPAR(3)=TFREEZ
    
    !ABRINDO ARQUIVOS PARA GRAVAÇÃO DE DADOS
    open( newunit = saidaFnum , file = 'results/SAIDA.TXT', action = 'write', status = 'replace')
    WRITE(saidaFnum,*) ' TEMPO   H     FO   dHdt     dFOdt'
    WRITE(*,*)         ' TEMPO   H     FO   dHdt     dFOdt'
    WRITE(saidaFnum,"(A, 5(F6.3, 1x))") " ", T, Y(:) !ESCREVE NO ARQUIVO DE SAÍDA O TEMPO, A ALTURA E A VAZÃO DE SAÍDA
    WRITE(*,"(A, 5(F6.3, 1x))") " ", T,Y(:)
    
    !ROTINA DE CHAMADA DA DASSL PARA INTEGRAÇÃO DO SISTEMA DE DAEs, EM PASSOS DE TEMPO DT, ATÉ O TEMPO FINAL TEND
    DO WHILE (T < TEND)
    CALL DDASSL (RES, NEQ, T, Y, YPRIME, TOUT, INFO, RTOL, ATOL, IDID, RWORK, LRW, IWORK, LIW, RPAR, IPAR, JAC) !INTEGRA O O SISTEMA
    TOUT=T+DT
    WRITE(saidaFnum,"(A, 5(F6.3, 1x))") " ", T,Y(:), YPRIME(:) !ESCREVE O TEMPO, A ALTURA E A VAZÃO DE SAÍDA E SUAS DERIVADAS
    WRITE(*,"(A, 5(F6.3, 1x))") " ", T,Y(:), YPRIME(:)
    ENDDO
    close(saidaFnum)
    !CONTINUE SEU APRENDIZADO LENDO O ARQUIVO "res.f90"
end program
